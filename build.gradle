task wrapper(type: Wrapper) {
	gradleVersion = '3.1'
}

apply plugin: 'java'
sourceCompatibility = 1.7
targetCompatibility = 1.7

apply plugin: 'maven'
apply plugin: 'jacoco'

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = {
	->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

ext.commonVersion = getVersionName()
version = "${commonVersion}".toString()

group = 'szue'

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
	classifier 'sources'
}

task listJars << {
    configurations.compile.each { File file -> println file.name }
}

artifacts {
	archives jar
	archives sourceJar
}

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.apache.pdfbox:pdfbox:2.0.3'
	compile 'org.slf4j:slf4j-api:1.7.21'
	compile 'com.google.guava:guava:20.0'
	compile 'org.apache.commons:commons-csv:1.4'
	testCompile 'junit:junit:4.12'
	testCompile 'commons-io:commons-io:2.5'
}

jacocoTestReport {
    reports {
        xml.enabled true
	html.enabled true
    }
}

